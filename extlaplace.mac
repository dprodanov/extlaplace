/**
Maxima package for Inverse Laplace transforms

http://eqworld.ipmnet.ru/en/auxiliary/aux-inttrans.htm


conventions:

	%variables: matching variables, global scope, suffer from side effects
	funcionp: predicate
	
	version 1.0 Date 17 Sept 2017
	
 * @license This library is free software; you can redistribute it and/or
 *      modify it under the terms of the GNU Lesser General Public
 *      License as published by the Free Software Foundation; either
 *      version 2.1 of the License, or (at your option) any later version.
 *
 *      This library is distributed in the hope that it will be useful,
 *      but WITHOUT ANY WARRANTY; without even the implied warranty of
 *      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *       Lesser General Public License for more details.
 *
 *      You should have received a copy of the GNU Lesser General Public
 *      License along with this library; if not, write to the Free Software
 *      Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 */
 
load("operpart.mac");
load("specfun.mac");

protectvars(expr, oper, lst):=block(
	buildq([lst, oper, expr],
		block (lst, apply(oper, expr) )
	)
);

diffp(expr):= inop(expr)=nounify('derivative);

lapsymb:nounify('laplace);

laplacexp (expr):=not freeof(lapsymb, expr);
ilaplacexp (expr):=not freeof('ilt, expr);

noundisp:true;
lispdisp:true;
/*grind :true;*/

/*
make sure that the variables are indefinite
*/
kill(%tvar, %lvar, %matchparams);

partbyvar(expr, var):= block([u],
	oppart(expr,  lambda([u], freeof(var, u)))
);

/*
 Laplace transform tweaks
*/
laparrange(expr):=block([xx, tt, ss, l, r, ret, opsubst : true ],
	if inop(expr)="+" then 
		ret:map(laparrange, expr)
	else (		
		ret:subst(lapsymb=lambda([xx, tt, ss], 
			([l, r]:partbyvar(xx, tt),
				l:subst(nil=1, l),
				r:subst(nil=1, r)
			),
			'laplace(r, tt, ss)*l
			),
		expr
		)
	),
	ret
);

/* Laplace transform manupulations */
lapsymsubst(expr, dvar):=block([xx, tt, ss, ee, ret], 
	ret:laparrange(expr),
	subst( lapsymb=lambda([xx, tt, ss], 
			ee:buildq([dvar], depends(dvar, ss)), 
			ev(ee, nouns),
			dvar), 
	ret) 
);	

/* [Pattern matching*/

/* 
nesting of patterns
*/
%matchparams:[];

_debug:false;

/*  boxed matching of predicates independent of variables*/
predmatch( expr, match):=block([lv, vv, ee],
	lv:propvars(matchdeclare),
	if not listp(expr) then
		ee:protectvars([expr], match, lv)
	else 
		ee:protectvars(expr, match, lv),
	if _debug then disp(ee),
	ee:ev(ee, nouns),
	if listp(ee) then (
		for vv in ee do (
			if _debug then display(vv),
			errcatch (
			%matchparams[lhs(vv)]:rhs(vv)
			)
			),
		true
	)else  ee
);

/*  boxed matching of predicates dependent of variables*/
predmatchv( expr, match, svar):=block([lv, vv, ee],
	lv:propvars(matchdeclare),
	if not listp(svar) then svar:[svar],
	if not listp(expr) then
		expr:append([expr], svar)
	else  expr:append(expr, svar),
	if _debug then display( expr), 
	if not listp(expr) then
		ee:protectvars([expr], match, lv)
	else 
		ee:protectvars(expr, match, lv),
	if _debug then disp(ee),
	ee:ev(ee, nouns),
	if listp(ee) then (
		for vv in ee do (
			if _debug then display(vv),
			errcatch (
			%matchparams[lhs(vv)]:rhs(vv)
			)
			),
		true
	)else  ee
);

/*matchdeclare(%x, true);
tellsimpafter(atanh(tanh(%x)), %x);
tellsimpafter(sin(%x)/cos(%x), tan(%x));
ellsimpafter(cos(%x)/sin(%x), cot(%x));*/

 /*  simple patterns*/
matchdeclare( 
	/* expressions*/
	[ %fun, %g, %h ], true, 
	/* variables*/
	[ %s, %t], lambda([u], symbolp (u) and u#%e and u#%pi and u#%gamma),
	/*  parameters, can be numeric*/
	[ %p, %q, %r ], mapatom
);

 /*  transform patterns*/
defmatch (lapargs, 'ilt(%fun, %s, %t) );
defmatch (lapargs2, 'laplace(%fun, %t, %s) );

defmatch (laphypgeo, %f[%p, %q] );

/*  integer-order integrator*/
defmatch (lapratio, %g*%s^(-1) );
/*  fractional integrator*/
defmatch (lapratiop, %fun*%s^(-%g) );
/*  fractional differentiator*/
defmatch (lapfrexp2,  %g*%s^%q );
defmatch (lapfrexp21,  %g*%s^%q, %s);
/*
patterns with radicals
presumably for bessel functions
*/
/* I/J pattern*/
defmatch (lapratiosqrt10, %fun*(%s^2 + %gg)^(-1/2-%g) );
defmatch (lapratiorad11, (%s^%g + %gg)^(-%h) );
defmatch (lapsqrt1, %fun*(%s^2 + %g)^(1/2) );
defmatch (lapsqrt2, %fun*(%s^2 + %p^2)^(1/2) );
defmatch (lapsqrt21, %fun*(%s^2 - %p^2)^(1/2) );
defmatch (lapratiop3, %fun*%s^(%g) );


/*  linear pattern for  scaling*/ 
defmatch (slinear, ( %p*%s + %gg ), %s);

 
matchdeclare( 
	%gg, 	 lambda([uu], freeof(%s, uu) and uu#0),
	%sg, 	 lambda([uu], not freeof(%s, uu) ),
	%srad1,   lambda([uu], predmatch( uu, lapratiorad11) ),
	%sqp1,    lambda([uu], predmatch( uu, lapsqrt1) ),
	%sqp11,   lambda([uu], predmatch( uu, lapsqrt21) ),
	%sqp12,   lambda([uu], predmatch( uu, lapsqrt2) ),
	%sratio,   lambda([uu], predmatch( uu, lapratio) ),
	%sratiop,  lambda([uu], predmatch( uu, lapratiop) ),
	%sratiop2, lambda([uu], predmatch( uu, lapratiop3) ),
	%sfr1,    lambda([uu], predmatch( uu,  lapfrexp2) )
);

defmatch (lapratiosqrt2, %fun*(%sqp1+ %s )^(-%q) ) ;
defmatch (lapratiosqrt21, %fun*(%sqp11+ %s )^(-%q) ) ;


/*
patterns with exponentials
presumably for error functions
delta function, unit step
*/
defmatch (lapexpp, %fun*%e^(%sg) );

defmatch (lapexprat2,  %sratiop*%e^(%sratio) ) ;
defmatch (lapexprat31,   %e^(%sfr1) ) ;
defmatch (lapexprat3,  %sratiop*%e^(%sfr1) ) ;

defmatch (lapratiop4, ( %p*%s + %g )^%h, %s);
matchdeclare(%sfr2,  lambda([uu],  predmatch( uu,  lambda([v], 'lapratiop4(v, %s)))));
defmatch (lapexprat33,  %e^(%gg*%sfr2), %s);
defmatch (lapexprat34,  %sratiop*%e^(%gg*%sfr2), %s);

/* pattern to detect shifing */

matchdeclare( %lprod,   lambda([uu],  coeff(uu, %s, 1)#0 and  coeff(uu, %s, 0)=0 ));

defmatch (lapexpp3, %fun*%e^(%lprod), %s );

ilapshiftarg(expr, %lvar, %tvar):=block([%fun, %lprod, a ],
	if  lapexpp3(expr, %lvar) # false  then ( 
		a:%lprod/%lvar,
		[unit(%tvar+a), %fun]
	) else [1, expr ]
);

/*  Miscelaneous patterns */
defmatch (lprod1, %s*%p, %s );

defmatch (lapexp4, %fun*%e^(%s*%gg), %s );

defmatch (laplog1,  %sratio*log(%s) ) ;

defmatch (lapatan, %fun*atan(%sratio) ); 
/*
hypergeometric patterns
*/
matchdeclare([%lsta, %lstb], listp); 
defmatch (laphgf,  %sratiop*%f[%p,%q](%lsta, %lstb, %sratio)) ;

/*
Mittag-Leffler patterns
*/
defmatch (lapefractee2,  %sratiop2*(%s^%q +%g)^(-%p))  ;
defmatch (lapefractee21,  %sratiop2*(%s^%g +%gg)^(-%h))  ;
defmatch (lapefractee20,  %sratiop2*%srad1)  ;
defmatch (lapefractee22,  (%s^%g +%gg)^(-%h));



sgn(expr):=if freeof(-1, expr) then 1 else -1;

ilapargs(expr):= block([ %fun, %s, %t], 
	if lapargs(expr)#false then [ %fun, %s, %t] else false
);

laprepare(expr):=block([ radexpand :all],
	if inop(expr)="+" then map(lambda([u], laprepare(u)), expr)
	else factor(radcan(expr))
);

/*  main  ILT functionality*/
ilaplace(expr):=block([l, r:1, l2:1, r2:1, %matchparams:[], fun, sfun, ret:false, %tvar, %lvar, ww, zz:1, sop],
	if ilaplacexp(expr) then (
		sop: inop(expr),
		if sop="+" then (
			/* removes side effects
			remarray(%matchparams),*/	
			ret:map(lambda([u], ww: laprepare (u), ilaplace(ww)), 
			expr)
		)else(			
			if sop=nounify('ilt) then (
				[fun, %lvar, %tvar]:ilapargs(expr)				
			) else (
				[l, r]:oppart(expr, ilaplacexp),
				/* display(l, r),*/
				[fun, %lvar, %tvar]:ilapargs(l)
			),
			/* shift  pattern injected here */
			[zz, sfun]: ilapshiftarg(fun, %lvar, %tvar),				
			/*display(sfun, zz, fun),*/
			if not numberp(sfun) and zz#1 then (
				[l2, r2]:subst(nil=1, partbyvar( sfun, %lvar)), 	
				/*display(l2, r2), */
				ret: r*l2* intablelapfun(r2, %lvar)
			) else 
				ret: r*intablelapfun(fun, %lvar)
		)
	)
	else ret:false,
	
	if freeof(false, ret) then
		ret:zz*ret else ret:expr,
	ret
);

/*
 main table  lookup functionality
*/
intablelapfun(expr, lvar):= block(
	[ops: inop(expr), ret:false, u, v, %matchparams, %p, %s, %q, %fun, %g, %gg ],
	display(expr),
	if symbolp(ops) then 
		ret:laptable[ops](expr)  
	elseif  predmatch( expr, lapratiosqrt10) then (
		disp("Bessel J, radical", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["bef2"](%matchparams['%gg], %matchparams['%g]),
		if besselexpand =true then
			ret:rootscontract(radcan(ret))
		)
	/*elseif  predmatch( expr, lapexprat2) then (
		disp("Bessel I/J, exp * frat ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["bef1"](%matchparams['%sratio], %matchparams['%sratiop]),
		if besselexpand =true then
			ret:rootscontract(radcan(ret))
		)*/
	elseif  predmatch( expr, lapratiosqrt2) then (
		disp("Bessel I/J,  1/t ", arrayinfo(%matchparams), listarray (%matchparams) ),
		if mapatom(%matchparams['%fun]) then
			ret:laptable["bef3"](%matchparams['%sqp1], %matchparams['%q]),
		if besselexpand =true then
			ret:rootscontract(radcan(ret))
	)
	elseif  predmatchv( expr, lapexp4, lvar) then (
		disp("Exp/Delta ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["delta"](%matchparams['%fun], %matchparams['%gg])
	)
	elseif  predmatch( expr, lapexprat31) then (
		disp("Exp/ Wright ", arrayinfo(%matchparams), listarray (%matchparams) ),
 		ret:laptable["exp1"](%matchparams['%sfr1], lvar)
		)
	elseif  predmatch( expr, lapexprat3) then (
		disp("Erfc/ Wright /fractional", arrayinfo(%matchparams), listarray (%matchparams) ),
		predmatch( %matchparams['%sratiop], lapratiop), 
		ret:laptable["erf1"](%matchparams['%sfr1], %matchparams['%sratiop], lvar)
	)
	elseif  predmatch( expr,   lambda([u],  'lapexprat33(u, lvar))) then (
		disp("Erfc/ Wright ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["exp2"](%matchparams['%sfr2], %matchparams['%gg], lvar)
	)
	elseif  predmatch( expr, lambda([u],  'lapexprat34(u, lvar))) then (
		disp("Erfc/ Wright /fractional", arrayinfo(%matchparams), listarray (%matchparams) ),
		predmatch( %matchparams['%sratiop], lapratiop), 
		ret:laptable["erf2"](%matchparams['%sfr2], %matchparams['%sratiop], %matchparams['%gg], lvar)
	)
	elseif  predmatch( expr, lapefractee2) then (
		disp("Mittag-Leffler 3/2/1 parameter ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["ml3"](%matchparams['%sratiop2],  %matchparams['%q], %matchparams['%p], %matchparams['%g])
	)
	elseif  predmatch( expr, lapefractee21) then (
		disp("Mittag-Leffler 3/2/1 parameter ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["ml3"](%matchparams['%sratiop2],  %matchparams['%g], %matchparams['%h], %matchparams['%gg])
	) elseif  predmatch( expr, lapefractee22) then (
		disp("Mittag-Leffler 3/2/1 parameter ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["ml31"]( %matchparams['%g], %matchparams['%h], %matchparams['%gg])
	)
	elseif  predmatch( expr, lapefractee20) then (
		disp("Mittag-Leffler 3/2/1 parameter ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["ml32"](%matchparams['%sratiop2], %matchparams['%srad1])
	) 
	elseif  predmatch(expr, laplog1) then (
		ret:laptable["log"](sgn(%matchparams['%sratio]))
	)
	elseif  predmatch( expr, laphgf) then (
		disp("hypergeometric ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["hgf"](%matchparams['%sratiop],  %matchparams['%sratio], %matchparams['%lsta], %matchparams['%lstb])
	)
	elseif  predmatch( expr, lapatan)  then (
		disp("atan ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:%matchparams['%fun] *laptable[atan](  %matchparams['%sratio])
	)
	elseif  predmatch( expr, lapratiop) then (
		disp("fractional power ", arrayinfo(%matchparams), listarray (%matchparams) ),
		ret:laptable["fp"](%matchparams['%fun],   %matchparams['%g])
	)
	else	ret:false,
	ret
);




/* fractional power*/
laptable["fp"]: lambda([u, v],  
	block([%fun, %p, %q, %s, lst], (
	if not mapatom(u) then false else (
		 %tvar^(v-1)/gamma(v)
		)
	)
	)
);

laptable["log"]: lambda([u],  u*( -log(%tvar)-%gamma) );

 /*
 hypergeometric
 %f [p,q] ([a],[b],z) 
 */
laptable["hgf"]: lambda([rp, rr, la, lb],  
	block( [ %s, %g, lst, p, a, %p, %fun, n, m, laa, lbb ],
		 (lapratiop(rp)), p:%g,
		 (lapratio(rr)), a:%g,
		 laa:copy(la),	 lbb:copy(lb),
		 n:length(la),	 m:length(lb),
		 laa[n]:laa[n]-p,
		 if laa[n]= 0 then (
			laa:rest(laa, -1),
			n:n-1
			),
		 hgfred(laa, lbb, a*%tvar) * %tvar^(p-1)/gamma(p)
		)
);

/* Mattag- Leffler 3 parameter*/
laptable["ml3"]: lambda([ uv, a, gam, g], 
	block([ %g, %fun, %g, %s,  b],
		disp(lapratiop3(uv)),
		b:-ratsimp(%g-a*gam),
		%fun: %tvar^(b-1) * %E[ a, b, gam](-g* %tvar^a),
		mlsimp(%fun)
	)
);

laptable["ml32"]: lambda([  sr, ra], 
	block([ %g, %fun, %h, %gg, %s, a, b],
		disp(lapratiorad11(ra)), gam:%h, g:%gg, a:%g,
		disp(lapratiop3(sr)), 
		b:-ratsimp(%g-a*gam),
		%fun: %tvar^(b-1) * %E[ a, b, gam](-g* %tvar^a),
		mlsimp(%fun)
	)
);

 
/* Erf related patterns 
 special functions
wright(alpha, beta )(x)
%M (alpha) (x) 
 Wright function
*/


laptable["exp1"]: lambda([u, ss], 
	block([ %g, %q, %s, k, %fun, q ],
		disp("exp / wright ", lapfrexp21(u, ss)),	k:%g, q:%q,
		%fun:k*q/%tvar^(1+q)*wright(-q, 1-q , 1/%tvar^(%q) * k),
	/*	if %q#1/2 then 
			k*%q/%tvar^(1+%q)*wright(-%q, 1- %q , -1/%tvar^(%q) * k)
		else 
			k/2/sqrt(%pi)/%tvar^(1+%q)*exp(-1/4/%tvar*k^2),*/

		wrightsimp(%fun)	
	)
);

laptable["exp2"]: lambda([u, k, ss], 
	block([ %h, %g, %q, %p, %s, q, tt, %fun, g ],
		disp("exp / wright ", lapratiop4(u, ss)), q:%h, g:%g,
		tt:%tvar/%p,
		%fun:k*q/%p*exp(-g*tt)/tt^(1+q)*wright( -q, 1- q , -1/tt^(q) * k),
		wrightsimp(%fun)
	)
);

laptable["erf1"]: lambda([u, v, ss],  
	block([ %fun, k, %s, %g, p ],
		if sgn(u)=1 then return(false),
		disp(lapfrexp21(u, ss)),
		k:%g,  q:%q,
		disp("erf1", lapratiop(v)), p:%g,
		/*display(q, p),*/
	/*	if q=1/2 and p=1 then 
			%fun:erfc(k/2*%tvar^(-q)),
		if q=1/2 and p=3/2 then 
			%fun:k*2/sqrt(%pi)*%tvar^(q)*exp(-1/4/%tvar*k^2)  -k*erfc(k/2*%tvar^(-q))
		else */
			%fun:1/%tvar^(1-p)*wright(-q, p, k/%tvar^q),
		wrightsimp(%fun)		
	)
);


laptable["erf2"]: lambda([u, v, k, ss],  
	block([ %fun, %p, %s, %g, p ],
		disp("exp / wright ", lapratiop4(u, ss)), q:%h, g:%g,
		tt:%tvar/%p, 
		 disp(lapratiop(v)), p:%g,
		/*	display(q, p, %p),
	if q=1/2 and p=1 then 
			%fun:erfc(k/2*%tvar^(-q)),
		else if q=1/2 and p=3/2 then 
			%fun:2/sqrt(%pi)*%tvar^(q)*exp(-1/4/%tvar*k^2)  -erfc(k/2*%tvar^(-q)),
		disp(%fun),*/
			%fun:exp(-g*tt)/tt^(1-p)*wright(-q, p, k/tt^q),
		wrightsimp(%fun)		
	)
);

/*
 lookup table closure
 and  interface pattern
*/
laptable[nil]:lambda([u], delta(%tvar)  );

laptable["delta"]:lambda([u, v], u*delta(%tvar+v)  );
/*
Lookup tables
*/

/*
 arctan
*/

laptable[atan]: lambda([ v],  
	block([  %p, %g, %s], 
	if op(v)=atan then v:first(args(v)),
		lapratio(v),
		sin(%g*%tvar)/%tvar
	)
);

/*	 
laptable["bef21"]: lambda([u, v], 
	 if v=-1/2 then false else sqrt(%pi)*%tvar^(v)*bessel_i(v, u*%tvar)/(2*u)^v/gamma(v+1/2) );
*/
/*   exp -> bessel_j  bessel_i*/

laptable["bef1"]: lambda([u, v],  
	block([ %fun, p, k, %s, %g  ],
		disp(lapratio(u)), 	k:%g,  
		disp(lapratiop(v)), p:%g,
		if not freeof(-1, k) then
			%fun:(-%tvar/k)^((p-1)/2) * bessel_j(p-1, 2*sqrt(-k * %tvar) )
		else
			%fun:(%tvar/k)^((p-1)/2) * bessel_i(p-1, 2*sqrt(k * %tvar) ),
		%fun
	)
);

laptable["bef2"]: lambda([u, v], block([ %fun,  k, %s, %q, %p  ],
	 if v=-1/2 then false else (
		if sgn(k)=1 then
			sqrt(%pi)*%tvar^(v)*bessel_j(v, sqrt(u)*%tvar)/(2*u)^v/gamma(v+1/2) 
		else
			sqrt(%pi)*%tvar^(v)*bessel_i(v, sqrt(-u)*%tvar)/(2*u)^v/gamma(v+1/2) 
		)
	 
	)
);

laptable["bef3"]: lambda([u, q],  
	block([ %fun, p, k, %s, %g, %p ],
		disp(lapsqrt1(u)), 	k:%g,  
		if not freeof(-1, k) then
			%fun: q*sqrt(-1/k)^q * bessel_i(q, sqrt(-k) * %tvar ) /%tvar
		else
			%fun: q/sqrt(k^q) * bessel_j(q,  sqrt(k) * %tvar ) /%tvar,
		%fun
	)
);

